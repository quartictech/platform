plugins {
    id "java"
    id "idea"
}

allprojects {
    apply from: "${rootDir}/gradle/dependencies.gradle"

    repositories {
        if (project.name.contains("weyl")) {
            // Has to go first because jai_core-1.1.3.jar isn't actually on MavenCentral
            maven {
                url "http://download.osgeo.org/webdav/geotools/"
            }
        }

        jcenter()

        if (project.name.contains("weyl")) {
            maven {
                url "https://github.com/ElectronicChartCentre/ecc-mvn-repo/raw/master/releases"
            }
        }
    }

    // NOTE: this needs to be applied before the test block below due to some strange ordering issues
    apply plugin: "java"
    test {
        maxHeapSize = "2048m"
    }

    sourceCompatibility = 1.8
    targetCompatibility = 1.8

    group "io.quartic.platform"
    version (System.env.CIRCLE_BUILD_NUM ?: "unknown")
}

idea {
    module {
        excludeDirs += file(".env")
    }
}
