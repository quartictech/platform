pluginManagement.repositories {
    maven {
        url "s3://quartictech-repo.s3.eu-west-2.amazonaws.com/maven2"
        credentials(AwsCredentials) {
            def fromFile = readFromCredsFile()
            accessKey System.getenv("AWS_ACCESS_KEY_ID") ?: fromFile["aws_access_key_id"]
            secretKey System.getenv("AWS_SECRET_ACCESS_KEY") ?: fromFile["aws_secret_access_key"]
        }
    }
    gradlePluginPortal()
}

def readFromCredsFile() {
    def fields = [:]
    def file = new File("${System.properties["user.home"]}/.aws/credentials")
    if (file.exists()) {
        file.eachLine { line ->
            def matcher = line =~ /^(aws_access_key_id|aws_secret_access_key)\s*=\s(.*)$/
            if (matcher) {
                fields[matcher[0][1]] = matcher[0][2]
            }
        }
    }
    return fields
}

// Ancillary

include "docs"

include "quarty"

include "common-core"
include "common-application"
include "common-test"

include "github"

// Services

include "bild"
include "bild-api"

include "catalogue"
include "catalogue-api"
include "catalogue-utils"

include "glisten"

include "home"
include "home-frontend"

include "howl"
include "howl-api"

include "rain"

include "registry"
include "registry-api"

include "scribe"

include "weyl"
include "weyl-api"
include "weyl-core"
include "weyl-frontend"

include "zeus"
include "zeus-frontend"
