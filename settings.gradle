pluginManagement.repositories {
    maven {
        url "s3://quartictech-repo.s3.eu-west-2.amazonaws.com/maven2"
        credentials(AwsCredentials) {
            def fromFile = readFromCredsFile()
            accessKey System.getenv("AWS_ACCESS_KEY_ID") ?: fromFile["aws_access_key_id"]
            secretKey System.getenv("AWS_SECRET_ACCESS_KEY") ?: fromFile["aws_secret_access_key"]
        }
    }
    gradlePluginPortal()
}

def readFromCredsFile() {
    def fields = [:]
    def file = new File("${System.properties["user.home"]}/.aws/credentials")
    if (file.exists()) {
        file.eachLine { line ->
            def matcher = line =~ /^(aws_access_key_id|aws_secret_access_key)\s*=\s(.*)$/
            if (matcher) {
                fields[matcher[0][1]] = matcher[0][2]
            }
        }
    }
    return fields
}


include "common-core"
include "common-application"
include "common-test"

include "catalogue-api"
include "catalogue-application"
include "catalogue-utils"

include "mgmt-application"
include "mgmt-frontend"

include "howl-api"
include "howl-application"

include "rain-application"
include "scribe"

include "weyl-api"
include "weyl-core"
include "weyl-application"
include "weyl-frontend"
