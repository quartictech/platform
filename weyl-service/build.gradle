plugins {
    id "java"
    id "idea"
    id "application"
    id "org.inferred.processors" version "1.2.3"
    id "com.palantir.docker" version "0.9.1"
    id "com.palantir.configuration-resolver" version "0.1.0"
}

dependencies {
    processor libraries.immutables

    compile project(":common-service")
    compile project(":weyl-core")
    compile project(":weyl-frontend")
    compile "io.dropwizard:dropwizard-jdbi:${versions.dropwizard}"
    compile "io.dropwizard:dropwizard-assets:${versions.dropwizard}"
    compile "com.liveperson:dropwizard-websockets:1.0.0-1"  // BEWARE! The highest version number in maven central is 1.2. This is actually an older version (and not compatible with DW 1.0)

    testCompile libraries.testCommon
}

jar {
    manifest {
        attributes("Implementation-Version": version)
    }
}

mainClassName = "io.quartic.weyl.WeylApplication"
applicationDefaultJvmArgs = ["-Xms8g", "-Xmx8g", "-XX:+PrintGCDetails", "-XX:+PrintGCDateStamps", "-Xloggc:mygclogfilename.gc"]

ext.memorySetting = (project.hasProperty("memory") ? project.memory : "8g")

run {
  args = ["server", "weyl.yml"]
  jvmArgs = ["-Xms${memorySetting}", "-Xmx${memorySetting}", "-XX:+PrintGCDetails", "-XX:+PrintGCDateStamps", "-Xloggc:mygclogfilename.gc"]
}

docker {
  name      "${System.env.QUARTIC_DOCKER_REPOSITORY}/${rootProject.name}:${version}"
  dependsOn distTar
  files     "build.gradle"    // Seems to be no way to say "no files", so using this as dummy
}
