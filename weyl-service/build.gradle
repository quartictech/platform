plugins {
    id 'org.inferred.processors' version '1.2.3'
    id 'com.palantir.docker' version '0.9.1'
    id "com.palantir.configuration-resolver" version "0.1.0"
}
apply plugin: 'java'
apply plugin: 'application'
apply plugin: 'idea'

dependencies {
  compile project(':weyl-core')
  compile project(':weyl-frontend')
  compile 'io.dropwizard:dropwizard-core:' + dropwizardVersion
  compile 'io.dropwizard:dropwizard-jdbi:' + dropwizardVersion
  compile 'io.dropwizard:dropwizard-assets:' + dropwizardVersion
  compile 'io.dropwizard.modules:dropwizard-java8:0.9.0-1'

  // BEWARE! The highest version number in maven central is 1.2. This is actually an older version (and not compatible with DW 1.0)
  compile 'com.liveperson:dropwizard-websockets:1.0.0-1'
  compile 'no.ecc.vectortile:java-vector-tile:' + vectorTileVersion
  compile 'org.postgresql:postgresql:9.4.1209'
  compile 'org.apache.commons:commons-io:1.3.2'
  processor 'org.immutables:value:2.3'

  testCompile 'junit:junit:4.12'
  testCompile 'org.hamcrest:hamcrest-all:1.3'
  testCompile 'org.mockito:mockito-core:2.2.5'
}

jar {
    manifest {
        attributes("Implementation-Version": version)
    }
}

mainClassName = 'io.quartic.weyl.WeylApplication'
applicationDefaultJvmArgs = ["-Xms8g", "-Xmx8g", '-XX:+PrintGCDetails', '-XX:+PrintGCDateStamps', '-Xloggc:mygclogfilename.gc']

ext.memorySetting = (project.hasProperty('memory') ? project.memory : '8g')

run {
  args = ['server', 'weyl.yml']
  jvmArgs = ["-Xms${memorySetting}", "-Xmx${memorySetting}", '-XX:+PrintGCDetails', '-XX:+PrintGCDateStamps', '-Xloggc:mygclogfilename.gc']
}

docker {
  name      "${project.dockerRepository}/${rootProject.name}:${version}"
  dependsOn distTar
  files     "build.gradle"    // Seems to be no way to say "no files", so using this as dummy
}
