apply plugin: "idea"

def env = [BUILD_VERSION: version]

node {
//    version = "8.1.2"
    yarnVersion = "0.27.0"
    download = "true"
}

yarn {
    // In a perfect world, we'd just put this in .yarnrc
    // However, see this: https://github.com/mapbox/mapbox-gl-js/issues/4885
    args =          ["--mutex", "network"]
}

task bundle(type: Exec) {
    inputs.files    yarn.outputs
    inputs.file     "${projectDir}/tsconfig.json"
    inputs.dir      "${projectDir}/config"
    inputs.dir      "${projectDir}/src"
    outputs.dir     "${buildDir}/webpack"

    environment  += ["NODE_ENV": "production"]      // TODO: this can probably be handled inside Webpack configuration

    commandLine     "${yarn.nodeModulesDir}/.bin/webpack",
                    "--config", "${projectDir}/config/webpack/prod.js",
                    "--env.node_modules_dir", yarn.nodeModulesDir,
                    "--progress", "--profile", "--colors"
}

task run(type: NodeTask) {
    inputs.files    yarn.outputs
    inputs.file     "${projectDir}/tsconfig.json"
    inputs.dir      "${projectDir}/config"
    inputs.dir      "${projectDir}/src"
    // No outputs because this should always run

    // TODO - how to inject cached node_modules dir?
    script        = file("${projectDir}/src/server")
}

task lint(type: Exec) {
    inputs.files    yarn.outputs
    inputs.file     "${projectDir}/tslint.json"
    inputs.dir      "${projectDir}/src"
    outputs.dir     "${buildDir}/lint"

    commandLine     "${yarn.nodeModulesDir}/.bin/tslint",
                    "-t", "stylish",
                    "-c", "${projectDir}/tslint.json",
                    "'${projectDir}/src/app/**/*.ts{,x}'"

    doFirst {
        file("${buildDir}/lint").mkdirs()
    }
}

jar {
    from bundle
}

check.dependsOn lint

idea {
    module {
        excludeDirs += file("node_modules") // TODO - replace with param
    }
}
